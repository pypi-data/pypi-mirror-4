grammar = stmts:s -> '%s\n' % '\n'.join(s)

block = ~~[anything*] !(self.indent()):i [stmts:s] !(self.dedent()) -> self.make_block(s, i)
funcblock = ~~[anything*] !(self.indent()):i [stmts:s] !(self.dedent()) -> self.make_func_block(s, i)
stmts = stmt*:ss -> self.make_stmts(ss)
stmt = (  ['while' while:e]
        | ['dowhile' dowhile:e]
        | ['if' if:e]
        | ['for' for:e]
        | ['try' try:e]
        | ['func' funcstmt:e]) -> '%s\n' % e
     | ['global' [expr*:names]] !(self.register_globals(names)) -> ''
     | ['nonlocal' [expr*:names]] !(self.register_nonlocals(names)) -> ''
     | ['pass'] -> ''
     | ['call' ~~(['name' 'JS'] anything*) call:e] -> e
     | ['comment' :c]    -> self.make_comment(c)
     | expr:e            -> '%s;' % e
expr = [:t apply(t):ans] -> ans

augassign :op expr:l expr:r !(self.register_var(l)) -> '%s %s %s' % (l, op, r)
assign expr:l expr:r !(self.register_var(l)) -> self.make_eq(l, r)

get     expr:x            -> x
getattr expr:x expr:y     -> '%s.%s' % (x, y)
getitem expr:x ( ['slice' (expr | anything):start
                  (expr | anything):end]                  -> '%s.slice(%s)' % (x, (start if start is not None else '0') + (', ' + end if end is not None else ''))
               | ['unop' '-'
                  [('number' | 'hexnumber') :i ?(i > 0)]] -> '%s.slice(%s)[0]' % (x, -i if i == 1 else '%s, %s' % (-i, -i+1))
               | expr:y                                   -> '%s[%s]' % (x, y)
               )
del     expr:x            -> 'delete %s' % x

unop     'not' ['call' ['name' 'hasattr'] [expr:obj expr:attr]] -> '(typeof %s[%s] == "undefined")' % (obj, attr)
unop     :op expr:x        -> '(%s%s)' % (self.op_map.get(op, op), x)

binop    'to' expr:x expr:y -> 'range(%s, %s+1)' % (x, y)
binop    'til' expr:x expr:y -> 'range(%s, %s)' % (x, y)
binop    :op expr:x expr:y -> '(%s %s %s)' % (x, op, y)
cmpop    expr:l [([:op expr:y] -> (op, y)):r] -> self.translate_cmp(l, r[0], r[1])
cmpop    expr:l [([:op expr:y] -> (op, y))+:r] -> self.make_chained_cmp(l, r)

name      :n       -> self.get_name(n)
number    :n       -> str(n)
hexnumber :n       -> hex(n)
string    :s       -> json.dumps(s)
rstring   :rs :m   -> '/%s/%s' % (json.dumps(rs)[1:-1], ''.join(m))

new  expr:c                  -> 'new %s' % c

call ['name' 'len'] [expr:obj] -> '%s.length' % obj
call ['name' 'type'] [expr:obj] -> 'typeof(%s)' % obj
call ['name' 'hasattr'] [expr:obj expr:attr] -> '(typeof %s[%s] != "undefined")' % (obj, attr)
call ['name' 'getattr'] [expr:obj expr:attr] -> '%s[%s]' % (obj, attr)
call ['name' 'setattr'] [expr:obj expr:attr expr:value] -> '%s[%s] = %s' % (obj, attr, value)
call ['name' 'JS'] ([['string' :js]] -> '%s' % js
                   |[expr:js]        -> 'eval(%s)' % js
                   )
call expr:fn [expr*:args]            -> '%s(%s)' % (fn, ', '.join(args))

list  !(self.indent()) expr*:xs !(self.dedent()) -> '[%s]' % ', '.join(xs)
tuple !(self.indent()) expr*:xs !(self.dedent()) -> '[%s]' % ', '.join(xs)
set   !(self.indent()) expr*:xs !(self.dedent()) -> 'set([%s])' % ', '.join(xs)
dict  !(self.indent()):i expr*:xs !(self.dedent()) -> self.make_dict(xs, i)

dictkv expr:k expr:v -> '%s: %s' % (k, v)

self            -> 'this'
break           -> 'break'
continue        -> 'continue'
return   expr:x -> 'return %s' % x
return          -> 'return'
raise    expr:x -> 'throw %s' % x

while expr:cond block:body -> 'while (%s) %s' % (cond, ''.join(body))
dowhile block:body expr:cond -> 'do %s while %s' % (''.join(body), cond)

if [expr:cond block:t] [([:cmts expr:c block:b] -> (cmts, c, b))*:ei] ([:cmts block:b] -> (cmts, b))?:e -> self.make_if(cond, t, ei, e)
ifexpr expr:cond expr:t expr:f -> '(%s ? %s : %s)' % (cond, t, f)

isliteral = ~~[('name' | 'number' | 'hexnumber' | 'string') anything*] -> True
          | -> False

for expr:var !(self.register_var(var))
    ( ['call' ['name' 'range'] [(isliteral:lit expr:e -> (lit, e))+:r ?(len(r) <= 3)]] block:body -> self.make_for_range(var, r, body)
    | ['call' ['name' 'reversed'] [['call' ['name' 'range'] [(isliteral:lit expr:e -> (lit, e))+:r ?(len(r) <= 3)]]]] block:body -> self.make_for_reversed_range(var, r, body)
    | expr:data block:body -> self.make_for(var, data, body)
    )

func funcstmt:f -> '(%s)' % f
funcstmt !(self.push_vars()) :name [expr*:args] !(self.register_globals(args)) funcblock:body !(self.pop_vars()) -> self.make_func(name, args, body)

try block:body ('except' [:cmts expr:err block:b]->[cmts, err, b])?:catch ('finally' [:cmts block:b]->[cmts, b])?:fin -> self.make_try(body, catch, fin)

